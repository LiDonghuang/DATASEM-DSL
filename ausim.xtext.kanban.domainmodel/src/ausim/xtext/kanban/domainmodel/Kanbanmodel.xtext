grammar ausim.xtext.kanban.domainmodel.Kanbanmodel with org.eclipse.xtext.common.Terminals

generate kanbanmodel "http://www.xtext.ausim/kanban/domainmodel/Kanbanmodel"

	KanbanSchedulingSystem:
		'KSSModel' name=ID
		
		'GovernanceModel'
		    ((Mechanisms +=Mechanism )+)?
		    ('GovernanceSearchStrategies'
		    (KSSSearchStrategies += Strategy)+
		    'end GovernanceSearchStrategies')?
			('TaskPatterns'
			(KSSTaskPatterns += TaskPattern)+
			(KSStaskPatternTypes += TaskType)+
			'end TaskPatterns')?
			('ValueFunctions'
			(KSSValueFunctions += ValueFunction)+
			'end ValueFunctions')?
		'end GovernanceModel'
		
		'OrganizationalModel'
			'ServiceTypes'
			(SoSServices += ServiceType)+
			'end ServiceTypes'
			'ServiceProviders'
			(orgUnits += ServiceProvider)+
			'end ServiceProviders'
		'end OrganizationalModel'
		
		'WorkItemNetworkModel'
			'WorkSources'
		    (kssSources += WorkSource)+
		    'end WorkSources'
		    'WorkItems'
			(kssWItems += WorkItem)+
			'end WorkItems'
	//		(kssWorkFlow=KanbanTaskModel)
		'end WorkItemNetworkModel'
		
		'ExperimentSettings'
			'Replications' replications=INT
			'InterArrivalTime' interArrivalTime=INT	
		'end ExperimentSettings'
		
		'end KSSModel'
	;
	

	

	// ------------------------ Governance Objects ---------------------------------
	TaskPattern:
		'workflowTemplate' name=ID '{'
		('Description' description=STRING)?
		('Types' '{' (taskpatternTypes+=[TaskType])+ '}')?
		'}'
	;
	TaskType :
		'taskType' name=ID '{'
		('Description' description=STRING)?
		'}'
	;	
	ValueFunction :
		'valueFunction' name=ID '{'
		('Description' description=STRING)?
		'}'
	;
	
	Mechanism:
		'Mechanism' name=ID
		('Description' description=STRING)?
		('ProcessType' processtype=STRING)
		(Strategies+=Strategy)+
	;	
	
	Strategy: 
		'Strategy' name=ID '{'
		('Description' description=STRING)?
	   	WIAcceptanceRule=WIAcceptance
	    WISelectionRule=WISelection 
	    WIAssignmentRule=WIAssignment 
	    ResourceAllocationRule=ResourceAllocation 
	    ResourceOutsourcingRule=ResourceOutsourcing   
	    '}'
	;
	
	
	WIAcceptance:
		'WIAcceptance' name=ID '{'
		('Description' description=STRING)?		
		('BackLogLimit' backlogQLimit=INT)?
		('WIPLimit' readyQLimit=INT)?
		'}'
	;
	WISelection:
		'WISelection' name=ID '{'
		('Description' description=STRING)?		
		'}'
	;
	WIAssignment:
		'WIAssignment' name=ID '{'
		('Description' description=STRING)?
		'}'
	;
	ResourceAllocation:
		'ResourceAllocation' name=ID '{'
		('Description' description=STRING)?
		'}'
	;
	ResourceOutsourcing:
		'ResourceOutsourcing' name=ID '{'
		('Description' description=STRING)?
		'}'
	;
	
    // ------------------------ WorkItem Objects ---------------------------------
    WorkSource:
    	'workSource' name=ID '{'
    	('Description' description=STRING)?
    	('targetUnits' '{' (targetUnits+=[ServiceProvider])+ '}')?
    	(assignmentRule=WIAssignment)?
    	'}'
    ;
//    WorkItemNetwork:
//    	'workItemNetwork' name=ID '{'
//    	('Description' description=STRING)?
//    	('workItems' '{' workItems+=WorkItem '}')
//    	'}'
//    ;
	WorkItem:
		'workItem' name=ID '{'
		('Description' description=STRING)?
		('Pattern' pattern=[TaskPattern])?		
		('Type' patternType=[TaskType])?
		('predecessors' '{' (pTasks+=[WorkItem])+ '}')?
		('subtasks' '{' (sTasks+=[WorkItem])+ '}')?
		('causality'(causalTriggers+=Causality)*)?
		('serviceType' (reqSpecialties+=[ServiceType])+)?
		('baseEfforts' befforts=INT)?
		('baseValue' bvalue=INT)?
		('localValues' lvalue+=LocalValue)?
		('classOfService' COS=ID)?
		('WorkSource' wItemSource=[WorkSource])?
		('arrivalTime' arrtime=INT)?
		('dueDate' duedate=INT)?
		'}'
	;
	Causality:
		('=>'| '->')'{'(triggered+=[WorkItem])+'}'
		('atProgress' tProgress=INT)?
		('onProbability' tProbability=INT)?		
	;
	LocalValue:
		('owner' owner=[ServiceProvider])
		('value' value=INT)
	;
	
	// ------------------------ Organizational Objects ---------------------------------
	ServiceType:
		'serviceType' name=ID ('{'
		('Description' description=STRING)?
		'}')?
	;	
	Service:
		'service' name=ID ('{'
		('Description' description=STRING)?
		('type' serviceType=[ServiceType])
		('efficiency' efficiency=INT)?
		'}')?
	;	
	ServiceProvider:
		('serviceProvider'|'group') name=ID '{'
		('Description' description=STRING)?
		('sourceUnits' '{' (sourceUnits+=[ServiceProvider])+ '}')?
		('targetUnits' '{' (targetUnits+=[ServiceProvider])+ '}')?
		('subordinateUnits' '{' (subordinateUnits+=[ServiceProvider])+ '}')?		
		('services' '{' (services+=Service)+ '}')?
		('governanceSearchStrategy' '{'
			'default' defaultStrategy=[Strategy]
			('specified' '{'
			(acceptanceRule=WIAcceptance)?
		    (selectionRule=WISelection)?
		    (assignmentRule=WIAssignment)?
		    (allocationRule=ResourceAllocation)?
		    (outsourcingRule=ResourceOutsourcing)?
		    '}')?
	    '}')?
	    ('resources' '{' (resources+=Resource)+ '}')?
	    '}'
	;
	Resource: 
		'resource' name=ID '{'
		('Description' description=STRING)?
		('services' '{' (services+=Service)+ '}')?
		'}'
	;	
	
	Asset: 
		'resource' name=ID '{'
		('Description' description=STRING)?
		('skills' '{' (hasSkills+=Skill)+ '}')?
		'}'
	;	
	Skill:
		name=ID ('{'
		('Description' description=STRING)?
		'}')?
	;
	
	
	// ------------------------ (Reserved Objects) ---------------------------------
	KanbanTaskModel:
		'WorkFlow' name=ID
		(caps+=Capability)+
		'end WorkFlow'
	;
	Capability:
		'capability' name=ID '{'
		('Description' description=STRING)?
		(reqs+=Requirement)+ 	
		'}'
	;
	Requirement:
		'requirement' name=ID '{' 
		 ('Description' description=STRING)?
		 ('workItems' '{' (rTasks+=[WorkItem])+ '}') 
		 ('workItemNetwork' (dependencies += Causality)*)?
		'}'
	;
	
	Entity:
		'entity' name=ID '{'
		('ServiceDirectory' sdname=ID '=' '['(serviceProvisions+=Provision)* ']')?
		(entityBehavior=Statemachine)
		'}'
	;
	
	Provision:
		'(' serviceName=[ServiceType] ',' '{'(providers+=[ServiceProvider])* '}'')' 
	;
	
	
	Statemachine :
     {Statemachine}
	('events' 
		events+=Event+ 
	'end')?
	('activities' 
		commands+=Command+ 
	'end')?
	(states+=State)*
;

Event:
	name=ID code=ID
;

Command:
	name=ID code=ID
;

State:
	'state' name=ID
		('actions' '{' actions+=[Command]+ '}')?
		(transitions+=Transition)*
	'end'
;

Transition:
	(event=[Event] '=>' state=[State]) | (event=[Event] '/' '[' (eventactions+=[Command])+ ']' '=>' state=[State])
;
	