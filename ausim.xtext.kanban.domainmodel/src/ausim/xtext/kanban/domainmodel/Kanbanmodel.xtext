grammar ausim.xtext.kanban.domainmodel.Kanbanmodel with org.eclipse.xtext.common.Terminals

generate kanbanmodel "http://www.xtext.ausim/kanban/domainmodel/Kanbanmodel"

	KanbanSchedulingSystem:
		'KSSWorkFlowModel' name=ID
		'Organization'
		(orgUnits += Team)+
		'end Organization'
		'WorkItems'
		(kssTasks += Task)+
		'end WorkItems'
		(kssWorkFlow=KanbanTaskModel)
		'end KSSWorkFlowModel'
	;
	
	
	Team :
		('team'|'group') name=ID '{'
		('sourceUnits' '{' (demandUnits+=[Team])+ '}')?
		('targetUnits' '{' (sourcedUnits+=[Team])+ '}')?
		('contains' '{' (groupmembers+=[Team])+ '}')?
		('resources' '{' (resources+=Asset)+ '}')?
		('services' '{' (services+=Service)+ '}')?
		'}'
	;
	

	Service:
		'service' name=ID
	;
	

	Asset : 
		'resource' name=ID ('{'
		'skills' '{' (hasSkills+=Skill)+ '}'
		'}')?
	;
	
	Skill:
		name=ID
	;
	
	
			
	Dependency:
		sourceTask=[Task] ('=>'| '->') targetTask=[Task] 

	;
		
	Requirement:
		'requirement' name=ID '{' 
		 ('workItems' '{' (rTasks+=[Task])+ '}') 
		 ('workItemNetwork' (dependencies += Dependency)*)?
		'}'
	;
	
	
	Task:
		'workItem' name=ID '{'
		('subtasks' '{' (sTasks+=[Task])+ '}')?
		('taskDependencies'(taskDependencies += Dependency)*)?
		('servicesRequired' (reqSpecialties+=[Service])+)?
		('base value' bvalue=INT)?
		('classOfService' COS=ID)?
		'}'
	;
	
	
	KanbanTaskModel:
		'TaskModel' name=ID
		(caps+=Capability)+
		'end TaskModel'
	;
	
	Capability:
		'capability' name=ID '{'
		(reqs+=Requirement)+ 	
		'}'
	;
	